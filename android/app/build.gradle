apply plugin: 'com.android.application'

// https://developer.android.com/studio/publish/app-signing#secure-shared-keystore
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def getGitRev = { ->
    def sha = new ByteArrayOutputStream()
    def dirty = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = sha
    }
    exec {
        commandLine 'sh', '-c', 'if test -n "`git status --porcelain --untracked=no`"; then echo "+"; fi'
        standardOutput = dirty
    }
    return sha.toString().trim() + dirty.toString().trim()
}

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 34
    defaultConfig {
        buildConfigField "String", "GitRev", "\"${getGitRev()}\""
        minSdkVersion 19
        targetSdkVersion 34
        versionCode 168
        versionName "3.1.8"
    }
    buildTypes {
        debug {
            ndk {
                abiFilters 'arm64-v8a', 'x86_64'
            }
            manifestPlaceholders = [appNameSuffix: " Debug"]
            applicationIdSuffix ".debug"
            //signingConfig signingConfigs.release
        }
        release {
            ndk {
                abiFilters 'arm64-v8a'
            }
            manifestPlaceholders = [appNameSuffix: ""]
            minifyEnabled false // R8 doesn't work, app crashes on startup
            signingConfig signingConfigs.release
        }
    }
    ndkVersion "26.1.10909125"
    externalNativeBuild {
        ndkBuild {
            path "src/main/cpp/Android.mk"
        }
    }
    flavorDimensions "floatType"
    productFlavors {
        decimal {
            getIsDefault().set(true)
            dimension "floatType"
            applicationId "com.thomasokken.free42"
            manifestPlaceholders = [appName:"Free42"]
            externalNativeBuild {
                ndkBuild {
                    arguments "FLOAT_TYPE=decimal"
                }
            }
        }
        binary {
            dimension "floatType"
            applicationId "com.thomasokken.free42b"
            manifestPlaceholders = [appName:"Free42 Binary"]
            externalNativeBuild {
                ndkBuild {
                    arguments "FLOAT_TYPE=binary"
                }
            }
        }
    }
    namespace 'com.thomasokken.free42'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
